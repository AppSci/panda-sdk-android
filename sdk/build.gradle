apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-parcelize'
apply plugin: 'kotlin-kapt'
apply plugin: 'maven-publish'

android {
    compileSdk 34

    resourcePrefix 'panda_'

    sourceSets {
        androidTest.assets.srcDirs += files("$projectDir/schemas".toString())
    }

    defaultConfig {
        minSdk 21
        targetSdk 34

        buildConfigField "String", "PANDA_ENDPOINT_PROD", "\"https://api.panda.boosters.company/\""
        buildConfigField "String", "PANDA_ENDPOINT_STAGE", "\"https://api.panda-stage.boosters.company/\""

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = ["room.schemaLocation": "$projectDir/schemas".toString()]
//                includeCompileClasspath = true
            }
        }

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            postprocessing {
                obfuscate false
                proguardFiles = ['proguard-android-optimize.txt', 'proguard-rules.pro']
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }
    kotlinOptions {
        jvmTarget = JavaVersion.VERSION_17.toString()
    }
    buildFeatures {
        viewBinding true
        buildConfig true
    }
    namespace 'com.appsci.panda.sdk'
}

def localProperties = new Properties()
localProperties.load(new FileInputStream(rootProject.file("local.properties")))

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                groupId = 'com.github.AppSci'
                artifactId = 'panda-sdk-android'
                version = '1.6.0'
            }
        }

        repositories {
            maven {
                name = "GitHubPackages"
                url = uri("https://maven.pkg.github.com/AppSci/panda-sdk-android")
                credentials {
                    setUsername(localProperties['github.actor'])
                    setPassword(localProperties['github.token'])
                }
            }
        }
    }
}

dependencies {
    implementation fileTree(dir: "libs", include: ["*.jar"])

    implementation presentationDependencies.lifecycleRuntime
    kapt presentationDependencies.lifecycleCompiler
    implementation presentationDependencies.fragment
    implementation presentationDependencies.fragmentKtx

    implementation supportDependencies.appCompat

    // Dagger 2
    implementation dataDependencies.dagger
    kapt dataDependencies.daggerCompiler

    // Kotlin dependencies
    implementation kotlinDependencies.kotlinStdLib
    implementation kotlinDependencies.androidKtx
    implementation kotlinDependencies.kotlinReflect

    // Rx + Coroutines
    implementation dataDependencies.rxJava
    implementation dataDependencies.rxAndroid
    implementation dataDependencies.rxKotlin
    implementation dataDependencies.rxBinding

    implementation dataDependencies.room
    implementation dataDependencies.roomRxJava
    kapt dataDependencies.roomCompiler

    implementation dataDependencies.threeten
    implementation dataDependencies.gson
    implementation dataDependencies.okHttp
    implementation dataDependencies.okHttpInterceptor
    implementation dataDependencies.retrofit
    implementation dataDependencies.retrofitGsonConverter
    implementation dataDependencies.retrofitRxJavaAdapter

    implementation platform('com.google.firebase:firebase-bom:32.7.0')
    implementation 'com.google.firebase:firebase-messaging-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    // Billing
    implementation dataDependencies.billingClient
    implementation dataDependencies.rxBilling

    implementation developmentDependencies.timber

    implementation coroutineDependencies.core
    implementation coroutineDependencies.android
    implementation coroutineDependencies.rx2


    testImplementation testDependencies.jupiterApi
    testImplementation testDependencies.jupiterEngine
    testImplementation testDependencies.jupiterParams

    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation androidTestDependencies.testRunner
    androidTestImplementation androidTestDependencies.espressoCore
    androidTestImplementation androidTestDependencies.supportAnnotations
    androidTestImplementation androidTestDependencies.testRoom, {
        exclude group: 'com.android.support'
    }
}
